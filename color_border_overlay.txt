// Color Border Overlay (pass-through of current input)

// copy into empty video processor plugin and eventually save as custom preset

//@param1:thick   'thickness (px)' 10 1 300 1
//@param2:red     'red'     1 0 1 0.5 0.1
//@param3:green   'green'   0 0 1 0.5 0.1
//@param4:blue    'blue'    0 0 1 0.5 0.1
//@param5:opacity 'opacity' 1 0 1 0.5 0.01
//@param6:pad     'inner padding (px)' 0 0 500 1

src = 0;

// project_w/h si vytáhni z inputu (ať sedí s tím, co teče do FX)
input_info(src, project_w, project_h);

// 1) nejdřív vykresli aktuální vstup (tuhle stopu)
gfx_blit(src, 1, 0, 0, project_w, project_h);

// 2) pak přes to rámeček
t = max(1, thick|0);
p = max(0, pad|0);
a = opacity < 0.001 ? 0 : opacity > 0.999 ? 1 : opacity;

x0 = p; y0 = p;
x1 = project_w - p;
y1 = project_h - p;

(x1 <= x0) ? (x1 = x0 + 1);
(y1 <= y0) ? (y1 = y0 + 1);

max_t = min((x1-x0)/2, (y1-y0)/2);
t > max_t ? t = max_t|0;

a > 0 ? (
  gfx_a = a;
  gfx_r = red; gfx_g = green; gfx_b = blue;

  w = x1 - x0;
  h = y1 - y0;

  gfx_fillrect(x0,       y0,       w, t);       // top
  gfx_fillrect(x0,       y1 - t,   w, t);       // bottom
  gfx_fillrect(x0,       y0 + t,   t, h - 2*t); // left
  gfx_fillrect(x1 - t,   y0 + t,   t, h - 2*t); // right
);